" Source config
" =============
" Set up vim dirs. {{{
let $VIMDIR = $HOME.(has('nvim') ? '/.config/nvim' : (has('unix') ? '/.vim' : '/vimfiles'))
" Backup, swap, undo files
if has('win32') || has('win64')
    " Create vim dirs if missing
    if !isdirectory($VIMDIR)
        silent !md ~/vimfiles/cache/backup
        silent !md ~/vimfiles/cache/view
        silent !md ~/vimfiles/cache/undo
        silent !md ~/vimfiles/pack/plugins/start
        silent !md ~/vimfiles/pack/plugins/opt
        silent !md ~/vimfiles/config/
    endif
else
    " Create vim dirs if missing
    if !isdirectory($VIMDIR)
        silent !mkdir -p $VIMDIR/cache/{backup,view} > /dev/null 2>&1
        silent !mkdir -p $VIMDIR/cache/undo > /dev/null 2>&1
        silent !mkdir -p $VIMDIR/pack/plugins/{start,opt} > /dev/null 2>&1
        silent !mkdir -p $VIMDIR/config/ > /dev/null 2>&1
    endif
endif
set backup
if has('persistent_undo')
    set undofile
    set undodir=$VIMDIR/cache/undo/
    if has('win32') || has('win64')
        set undodir-=~/.vim/cache/undo/
        set undodir^=~/vimfiles/cache/undo/
    endif
endif
set backupdir=$VIMDIR/cache/backup//   " For backup files
set directory=$VIMDIR/cache/backup//   " For .swp files
set backupskip=/tmp/*                  " Add some paths not to back up
set backupskip^=/dev/shm/*             " Shared memory RAM disk
set backupskip^=/var/tmp/*             " Debian's $TMPDIR for sudoedit(8)
if !has('unix')
    set backupskip-=/dev/shm/*
    set backupskip-=/var/tmp/*
endif
set history=2000
set undolevels=2000                    " Number of undos
set undoreload=8000                    " Number of lines to save for undo
" }}}

" Source any .vim files from config dir.
runtime! config/*.vim
" config/settings.vim
" config/plugins.vim
" config/mappings.vim

" Todo {{{
" ====
" https://github.com/junegunn/dotfiles/blob/master/vimrc#L856
function! s:todo() abort
    let entries = []
    for cmd in ['git grep -niI -e TODO -e FIXME -e XXX 2> /dev/null',
            \ 'grep -rniI -e TODO -e FIXME -e XXX * 2> /dev/null']
        let lines = split(system(cmd), '\n')
        if v:shell_error != 0 | continue | endif
        for line in lines
            let [fname, lno, text] = matchlist(line, '^\([^:]*\):\([^:]*\):\(.*\)')[1:3]
            call add(entries, { 'filename': fname, 'lnum': lno, 'text': text })
        endfor
        break
    endfor
    if !empty(entries)
        call setqflist(entries)
        copen
    endif
endfunction
command! Todo call s:todo()
nnoremap <leader>t :Todo<space><cr>
" }}}
